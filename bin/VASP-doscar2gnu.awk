#!/bin/awk -f
BEGIN{ 
  outf=  ARGV[1]".dat"
  outps= ARGV[1]".ps"
  print "Data tabulated in : "outf
  print "PostScript plot in: "outps
}

NR==1 {natoms=$1}

NR==6 {Emax=$1; Emin=$2; n= $3; efermi=$4
  print "#Generated by VASP-doscar2gnu.awk Efermi= "efermi > outf

  for (iatom=0; iatom <= natoms; iatom++){
    print "# atom= "iatom"  data_points: "n >> outf
    for (i =1; i<=n;i++){
      getline;
      if (iatom == 0) print $1, $2, $3 >> outf
      else  print $1, $2, $3, $4       >> outf
    }
    getline
    print "\n" >> outf
  }
}

END{
  cmd= "gnuplot "
  print "set term post color enhanced solid" | cmd
  print "set out \""outps"\"" | cmd
  print "set xlabel \"Energy (eV)\"" | cmd
  print "set ylabel \"Arbitrary units\"" | cmd

  print "set arrow from "efermi", 0 rto graph 0, 0.9 nohead lw 0.5" | cmd
  print "set title \"Total DOS\"" | cmd
  print "plot \"DOSCAR.dat\" i 0 w l t \"Total\"" | cmd

  for (i=1; i<= natoms; i++){
    print "set title \"Atom index: "i"\"" | cmd
    print "plot \"DOSCAR.dat\" i "i" u 1:2 w l t \"s\", \"\" i "i" u 1:3 w l t \"p\", \"\" i "i" u 1:4 w l t \"d\"" | cmd
  }
}


